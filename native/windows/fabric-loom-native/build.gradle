plugins {
	id 'cpp-library'
	id 'cpp-unit-test'
	id 'fabric-cpp-formatting'
}

library {
	targetMachines = [
			machines.windows.x86, machines.windows.x86_64, machines.windows.architecture("aarch64")
	]
}

library {
	linkage = [
		Linkage.SHARED
	]
}

import org.gradle.internal.jvm.Jvm
def jvmHome = Jvm.current().javaHome

def javaProject = project(":fabric-loom-native")

tasks.withType(CppCompile).configureEach {
	includes.from(new File(jvmHome, "include"))
	includes.from(new File(jvmHome, "include/win32"))
	includes.from(javaProject.getLayout().buildDirectory.dir("generated/sources/headers/java/main"))

	compilerArgs.addAll("/std:c++20", "/EHsc", "/W4", "/WX", "/MP")

	dependsOn javaProject.getTasks().named("compileJava")

	if (it.name.startsWith("compileTest") || it.name.startsWith("compileDebug")) {
		compilerArgs.addAll("/D_DEBUG", "/MDd")
		debuggable = true
	}
}

tasks.withType(AbstractLinkTask).configureEach {
	linkerArgs.addAll("Rstrtmgr.lib", "User32.lib", "/OPT:REF")
}

unitTest {
	dependencies {
		implementation project(":native:third-party:google-test")
	}
}
